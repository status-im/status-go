pipeline {
  agent { label 'linux' }

  parameters {
    string(
      name: 'BRANCH',
      defaultValue: 'develop',
      description: 'Name of branch to build.'
    )
    booleanParam(
      name: 'RELEASE',
      defaultValue: false,
      description: 'Enable to create build for release.',
    )
  }

  options {
    timestamps()
    disableConcurrentBuilds()
    /* Go requires a certain directory structure */
    checkoutToSubdirectory('src/github.com/status-im/status-go')
    /* manage how many builds we keep */
    buildDiscarder(logRotator(
      numToKeepStr: '5',
      daysToKeepStr: '30',
    ))
  }

  environment {
    /* fix for gomobile complaining about missing packages */
    CGO_ENABLED      = "1"
    GO111MODULE      = "off"
    /* Other stuff */
    BUILD_PLATFORM   = 'android'
    STATUS_PATH      = "${env.WORKSPACE}/src/github.com/status-im/status-go"
    CI_DIR           = "${env.STATUS_PATH}/_assets/ci"
    GOPATH           = "${env.WORKSPACE}"
    PATH             = "/usr/local/go/bin:${env.PATH}:${env.GOPATH}/bin"
    ANDROID_HOME     = '/usr/lib/android-sdk'
    ANDROID_SDK_ROOT = '/usr/lib/android-sdk'
    /* gomobile requires a specific NDK version */
    ANDROID_NDK      = "${env.NDK_GOMOBILE}"
    ANDROID_NDK_HOME = "${env.NDK_GOMOBILE}"
  }

  stages {
    stage('Prep') { steps { script {
      lib = load("${CI_DIR}/lib.groovy")
      /* clarify what we're building */
      println("Version: ${lib.getVersion()}")
      println("Git Branch: ${lib.gitBranch()}")
      println("Git Commit: ${lib.gitCommit()}")
      /* save and create a dir for artifacts */
      dest = "${env.WORKSPACE}/out"
      sh "mkdir -p ${dest}"
      /* for easier reuse */
      artifact = "status-go-android-${lib.suffix()}.aar"
    } } }

    stage('Setup') { steps { dir(env.STATUS_PATH) {
      sh 'make setup-build modvendor-install'
    } } }

    stage('Vendoring check') { steps { dir(env.STATUS_PATH) {
      /* fail build if vendoring hasn't been done */
      sh 'GO111MODULE=on make vendor && git diff --exit-code --no-color --stat vendor/'
    } } } // stage(Vendoring check)

    stage('Compile') { steps { dir(env.STATUS_PATH) {
      sh 'make statusgo-android'
      sh "cp build/bin/statusgo.aar ${dest}/${artifact}"
    } } }

    stage('Archive') { steps {
      archiveArtifacts("out/${artifact}")
    } }

    stage('Upload') { steps { script {
      env.PKG_URL = lib.uploadArtifact("out/${artifact}")
    } } }
  } // stages
  post {
    success { script { load("${CI_DIR}/ghcmgr.groovy").postBuild(true) } }
    failure { script { load("${CI_DIR}/ghcmgr.groovy").postBuild(false) } }
    always { dir(env.STATUS_PATH) {
      sh 'make clean'
      sh "rm -fr ${dest}"
    } }
  } // post
} // pipeline
